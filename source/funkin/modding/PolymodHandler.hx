package funkin.modding;

import polymod.fs.ZipFileSystem;
import funkin.data.dialogue.ConversationRegistry;
import funkin.data.dialogue.DialogueBoxRegistry;
import funkin.data.dialogue.SpeakerRegistry;
import funkin.data.event.SongEventRegistry;
import funkin.data.story.level.LevelRegistry;
import funkin.data.notestyle.NoteStyleRegistry;
import funkin.play.notes.notekind.NoteKindManager;
import funkin.data.song.SongRegistry;
import funkin.data.freeplay.player.PlayerRegistry;
import funkin.data.freeplay.style.FreeplayStyleRegistry;
import funkin.data.stage.StageRegistry;
import funkin.data.stickers.StickerRegistry;
import funkin.data.freeplay.album.AlbumRegistry;
import funkin.modding.module.ModuleHandler;
import funkin.data.character.CharacterData.CharacterDataParser;
import funkin.save.Save;
import funkin.util.FileUtil;
import funkin.util.macro.ClassMacro;
import funkin.mod.FunkinConverter;
import funkin.mod.FunkinConverter.FunkinModFormat;
import funkin.mod.FunkinConverter.ConversionReport;
import polymod.backends.PolymodAssets.PolymodAssetType;
import polymod.format.ParseRules.TextFileFormat;
import polymod.Polymod;

enum ModApiTier
{
  Engine;
  Support;
  Unsupported;
}

@:nullSafety
class PolymodHandler
{
  public static var API_VERSION(get, never):String;

  static function get_API_VERSION():String
  {
    return Constants.VERSION;
  }

  public static final API_VERSION_ENGINE:String = '0.1.0';
  public static final API_VERSION_RULE_ENGINE:String = '=0.1.0';
  public static final API_VERSION_RULE_SUPPORT:String = '>=0.1.0 <=0.9.0';
  public static final API_VERSION_RULE:String = '>=0.1.0 <=0.9.0';
  static final CONVERTED_SUFFIX:String = '_psych_converted';
  static final MOD_FOLDER:String =
    #if (REDIRECT_ASSETS_FOLDER && mac)
    '../../../../../../../example_mods'
    #elseif REDIRECT_ASSETS_FOLDER
    '../../../../example_mods'
    #else
    'mods'
    #end;
  static final CORE_FOLDER:Null<String> =
    #if (REDIRECT_ASSETS_FOLDER && mac)
    '../../../../../../../assets'
    #elseif REDIRECT_ASSETS_FOLDER
    '../../../../assets'
    #else
    null
    #end;
  public static var loadedModDirs:Array<String> = [];
  public static var loadedModIds:Array<String> = [];
  public static var modApiTiers:Map<String, ModApiTier> = new Map();
  public static var modFormats:Map<String, FunkinModFormat> = new Map();
  public static var conversionReports:Map<String, ConversionReport> = new Map();
  static var modFileSystem:Null<ZipFileSystem> = null;
  static var cachedModMetadata:Null<Array<ModMetadata>> = null;

  public static function createModRoot():Void
  {
    FileUtil.createDirIfNotExists(MOD_FOLDER);
  }

  /**
   * Returns the physical folder where mods are stored.
   *
   * Used by systems which need direct filesystem access,
   * such as the Lua module loader.
   */
  public static function getModFolder():String
  {
    return MOD_FOLDER;
  }

  public static function loadAllMods():Void
  {
    #if sys
    createModRoot();
    #end

    loadModsByDir(getAllModDirs());
  }

  public static function loadEnabledMods():Void
  {
    #if sys
    createModRoot();
    #end

    loadModsByDir(Save.instance.enabledModDirs.value);
  }

  public static function loadNoMods():Void
  {
    #if sys
    createModRoot();
    #end

    loadModsByDir([]);
  }

  public static function loadModsByDir(dirs:Array<String>):Void
  {
    buildImports();
    refreshModCache();

    var resolvedDirs:Array<String> = resolveModDirs(dirs);

    if (modFileSystem == null) modFileSystem = buildFileSystem();

    var loadedModList:Array<ModMetadata> = polymod.Polymod.init({
      modRoot: MOD_FOLDER,
      dirs: resolvedDirs,
      framework: OPENFL,
      apiVersionRule: API_VERSION_RULE,
      errorCallback: PolymodErrorHandler.onPolymodError,
      customFilesystem: modFileSystem,
      frameworkParams: buildFrameworkParams(),
      ignoredFiles: buildIgnoreList(),
      parseRules: buildParseRules(),
      skipDependencyErrors: true,
      useScriptedClasses: true,
      loadScriptsAsync: #if html5 true #else false #end,
    });

    loadedModIds = [];
    loadedModDirs = [];
    modApiTiers = new Map();

    if (loadedModList != null)
    {
      for (mod in loadedModList)
      {
        loadedModDirs.push(mod.dirName);
        loadedModIds.push(mod.id);

        var tier:ModApiTier = classifyModApiVersion(mod.apiVersion);

        modApiTiers.set(mod.dirName, tier);

        switch (tier)
        {
          case Engine:
            FlxG.log.add('[Polymod] "${mod.id}" targets the native engine API (${mod.apiVersion}).');

          case Support:
            FlxG.log.add('[Polymod] "${mod.id}" targets the compatibility API (${mod.apiVersion}), running in support mode.');

          case Unsupported:
            FlxG.log.warn('[Polymod] "${mod.id}" declares an unrecognized API version (${mod.apiVersion}).');
        }
      }
    }
  }

  #if sys
  static function resolveModDirs(dirs:Array<String>):Array<String>
  {
    return[for (dir in dirs) resolveModDir(dir)];
  }

  static function resolveModDir(dirName:String):String
  {
    if (StringTools.endsWith(dirName, CONVERTED_SUFFIX)) return dirName;

    var modPath:String = '$MOD_FOLDER/$dirName';

    var format:FunkinModFormat = FunkinConverter.detectFormat(modPath);

    modFormats.set(dirName, format);

    if (format != Psych) return dirName;

    var convertedDirName:String = '$dirName$CONVERTED_SUFFIX';
    var convertedPath:String = '$MOD_FOLDER/$convertedDirName';

    if (!sys.FileSystem.exists(convertedPath))
    {
      FlxG.log.add('[Polymod] Detected Psych Engine mod "$dirName", converting automatically...');

      var report:ConversionReport = FunkinConverter.convertMod(modPath, convertedPath);

      conversionReports.set(dirName, report);

      FlxG.log.add(
        '[Polymod] Converted "$dirName": ' + '${report.songsConverted.length} songs, ' + '${report.charactersConverted.length} characters, ' +
        '${report.stagesConverted.length} stages, ' + '${report.weeksConverted.length} weeks.'
      );

      for (error in report.errors)
      {
        FlxG.log.warn('[Polymod] Conversion issue for "$dirName": $error');
      }
    }

    return convertedDirName;
  }

  public static function forceReconvertMod(dirName:String):Void
  {
    var convertedPath:String = '$MOD_FOLDER/$dirName$CONVERTED_SUFFIX';

    if (sys.FileSystem.exists(convertedPath)) deleteDirectoryRecursive(convertedPath);

    conversionReports.remove(dirName);
  }

  static function deleteDirectoryRecursive(path:String):Void
  {
    if (!sys.FileSystem.exists(path)) return;

    for (entry in sys.FileSystem.readDirectory(path))
    {
      var full:String = '$path/$entry';

      if (sys.FileSystem.isDirectory(full))
      {
        deleteDirectoryRecursive(full);
      }
      else
      {
        sys.FileSystem.deleteFile(full);
      }
    }

    sys.FileSystem.deleteDirectory(path);
  }
  #else
  static function resolveModDirs(dirs:Array<String>):Array<String>
  {
    return dirs;
  }
  #end

  public static function getModFormat(dirName:String):FunkinModFormat
  {
    var format:Null<FunkinModFormat> = modFormats.get(dirName);

    return format == null ? Native : format;
  }

  public static function classifyModApiVersion(version:Null<String>):ModApiTier
  {
    if (version == null) return Unsupported;

    var parts:Array<String> = version.split('.');

    if (parts.length < 3) return Unsupported;

    var major:Null<Int> = Std.parseInt(parts[0]);
    var minor:Null<Int> = Std.parseInt(parts[1]);
    var patch:Null<Int> = Std.parseInt(parts[2]);

    if (major == null || minor == null || patch == null) return Unsupported;

    var majorInt:Int = major;
    var minorInt:Int = minor;
    var patchInt:Int = patch;

    if (majorInt == 0 && minorInt == 0 && patchInt == 1) return Engine;

    if (majorInt == 0 && minorInt >= 1 && minorInt <= 9 && patchInt == 0) return Support;

    return Unsupported;
  }

  public static function isEngineMod(dirName:String):Bool
  {
    return modApiTiers.get(dirName) == Engine;
  }

  public static function isSupportMod(dirName:String):Bool
  {
    return modApiTiers.get(dirName) == Support;
  }

  public static function getModApiTier(dirName:String):ModApiTier
  {
    var tier:Null<ModApiTier> = modApiTiers.get(dirName);

    return tier == null ? Unsupported : tier;
  }

  static function buildFileSystem():polymod.fs.ZipFileSystem
  {
    polymod.Polymod.onError = PolymodErrorHandler.onPolymodError;

    return new ZipFileSystem({
      modRoot: MOD_FOLDER,
      autoScan: true
    });
  }

  static function blacklistClasses(classes:List<Class<Dynamic>>, ?skipIf:String->Bool):Void
  {
    for (cls in classes)
    {
      if (cls == null) continue;

      var className:String = Type.getClassName(cls);

      if (skipIf != null && skipIf(className)) continue;

      Polymod.blacklistImport(className);
    }
  }

  static function buildImports():Void
  {
    static final DEFAULT_IMPORTS:Array<Class<Dynamic>> = [
      funkin.Assets,
      funkin.Paths,
      funkin.Preferences,
      funkin.util.Constants,
      flixel.FlxG,
      funkin.modding.api.MoonAPI,
      funkin.modding.api.MoonSong,
      funkin.modding.api.MoonPlayer,
      funkin.modding.api.MoonCamera,
      funkin.modding.api.MoonCharacter,
      funkin.modding.api.MoonInput,
      funkin.modding.api.MoonVars,
      funkin.modding.api.MoonTimers,
      funkin.modding.api.MoonAudio,
      funkin.modding.api.MoonTween,
      funkin.modding.api.MoonUI,
      funkin.modding.api.MoonModchart
    ];

    for (cls in DEFAULT_IMPORTS)
    {
      Polymod.addDefaultImport(cls);
    }

    Polymod.addImportAlias('lime.utils.Assets', funkin.Assets);

    Polymod.addImportAlias('openfl.utils.Assets', funkin.Assets);

    Polymod.addImportAlias('funkin.modding.base.ScriptedFunkinSprite', funkin.graphics.ScriptedFunkinSprite);

    Polymod.addImportAlias('funkin.modding.base.ScriptedMusicBeatState', funkin.ui.ScriptedMusicBeatState);

    Polymod.addImportAlias('funkin.modding.base.ScriptedMusicBeatSubState', funkin.ui.ScriptedMusicBeatSubState);

    Polymod.addImportAlias('funkin.data.dialogue.conversation.ConversationRegistry', funkin.data.dialogue.ConversationRegistry);

    Polymod.addImportAlias('funkin.data.dialogue.dialoguebox.DialogueBoxRegistry', funkin.data.dialogue.DialogueBoxRegistry);

    Polymod.addImportAlias('funkin.data.dialogue.speaker.SpeakerRegistry', funkin.data.dialogue.SpeakerRegistry);

    Polymod.addImportAlias('funkin.play.character.CharacterDataParser', funkin.data.character.CharacterData.CharacterDataParser);

    Polymod.addImportAlias('funkin.play.character.CharacterData.CharacterDataParser', funkin.data.character.CharacterData.CharacterDataParser);

    Polymod.addImportAlias('funkin.graphics.adobeanimate.FlxAtlasSprite', funkin.graphics.FunkinSprite);

    Polymod.addImportAlias('funkin.modding.base.ScriptedFlxAtlasSprite', funkin.graphics.ScriptedFunkinSprite);

    Polymod.addImportAlias('funkin.util.FileUtil', funkin.util.FileUtilSandboxed);

    #if FEATURE_NEWGROUNDS
    Polymod.addImportAlias('funkin.api.newgrounds.Leaderboards', funkin.api.newgrounds.Leaderboards.LeaderboardsSandboxed);

    Polymod.addImportAlias('funkin.api.newgrounds.Medals', funkin.api.newgrounds.Medals.MedalsSandboxed);

    Polymod.addImportAlias('funkin.api.newgrounds.NewgroundsClient', funkin.api.newgrounds.NewgroundsClient.NewgroundsClientSandboxed);
    #end

    Polymod.addImportAlias('funkin.api.discord.DiscordClient', funkin.api.discord.DiscordClient.DiscordClientSandboxed);

    Polymod.blacklistImport('Sys');

    Polymod.addImportAlias('Reflect', funkin.util.ReflectUtil);

    Polymod.addImportAlias('Type', funkin.util.ReflectUtil);

    Polymod.blacklistImport('cpp.Lib');
    Polymod.blacklistImport('haxe.Http');
    Polymod.blacklistImport('haxe.Unserializer');
    Polymod.blacklistImport('lime.utils.AssetLibrary');
    Polymod.blacklistImport('lime.system.CFFI');
    Polymod.blacklistImport('lime.system.JNI');
    Polymod.blacklistImport('lime.system.System');
    Polymod.blacklistImport('lime.utils.Assets');
    Polymod.blacklistImport('openfl.utils.Assets');
    Polymod.blacklistImport('openfl.Lib');
    Polymod.blacklistImport('openfl.system.ApplicationDomain');
    Polymod.blacklistImport('openfl.net.SharedObject');
    Polymod.blacklistImport('openfl.desktop.NativeProcess');

    Polymod.blacklistStaticFields(flixel.util.FlxSave, ['resolveFlixelClasses']);

    Polymod.blacklistStaticFields(flixel.FlxG, ['save']);

    Polymod.blacklistStaticFields(haxe.Unserializer, ['run']);

    Polymod.blacklistInstanceFields(haxe.Unserializer, ['unserialize']);

    Polymod.blacklistInstanceFields(funkin.save.Save, [
      'data',
      'clearData',
      'setLevelScore',
      'setSongScore',
      'applySongRank'
    ]);

    #if !html5
    Polymod.blacklistInstanceFields(openfl.filesystem.FileStream, ['readObject']);
    #end

    Polymod.blacklistInstanceFields(openfl.net.Socket, ['readObject']);

    Polymod.blacklistInstanceFields(openfl.utils.ByteArray.ByteArrayData, ['readObject']);

    blacklistClasses(
      ClassMacro.listClassesInPackage('funkin.api'),
      (className) -> polymod.hscript._internal.PolymodScriptClass.importOverrides.exists(className)
    );

    blacklistClasses(ClassMacro.listClassesInPackage('polymod'));

    blacklistClasses(ClassMacro.listClassesInPackage('hscript'));

    blacklistClasses(ClassMacro.listClassesInPackage('io.newgrounds'));

    blacklistClasses(ClassMacro.listClassesInPackage('sys'));

    blacklistClasses(ClassMacro.listClassesInPackage('funkin.util.macro'));

    Polymod.blacklistImport('funkin.external.android.CallbackUtil');

    Polymod.blacklistImport('funkin.external.android.DataFolderUtil');

    Polymod.blacklistImport('funkin.external.android.JNIUtil');

    Polymod.blacklistInstanceFields(polymod.hscript._internal.PolymodScriptClass.PolymodScriptClass, ['_interp']);
  }

  static function buildIgnoreList():Array<String>
  {
    var result:Array<String> = Polymod.getDefaultIgnoreList();

    result.push('.vscode');
    result.push('.idea');
    result.push('.git');
    result.push('.gitignore');
    result.push('.gitattributes');
    result.push('README.md');

    return result;
  }

  static function buildParseRules():polymod.format.ParseRules
  {
    var output:polymod.format.ParseRules = polymod.format.ParseRules.getDefault();

    output.addType('txt', TextFileFormat.LINES);

    return output;
  }

  static inline function buildFrameworkParams():polymod.Polymod.FrameworkParams
  {
    return {
      assetLibraryPaths: [
        'default' => 'preload',
        'shared' => 'shared',
        'songs' => 'songs',
        'videos' => 'videos',
        'tutorial' => 'tutorial',
        'week1' => 'week1',
        'week2' => 'week2',
        'week3' => 'week3',
        'week4' => 'week4',
        'week5' => 'week5',
        'week6' => 'week6',
        'week7' => 'week7',
        'weekend1' => 'weekend1',
        'sserafim' => 'sserafim'
      ],

      coreAssetRedirect: CORE_FOLDER,
    };
  }

  public static function refreshModCache():Void
  {
    cachedModMetadata = null;
  }

  public static function getAllMods(forceRescan:Bool = false):Array<ModMetadata>
  {
    if (!forceRescan && cachedModMetadata != null) return cachedModMetadata;

    if (modFileSystem == null) modFileSystem = buildFileSystem();

    var modMetadata:Array<ModMetadata> = Polymod.scan({
      modRoot: MOD_FOLDER,
      apiVersionRule: API_VERSION_RULE,
      fileSystem: modFileSystem,
      errorCallback: PolymodErrorHandler.onPolymodError
    });

    var filtered:Array<ModMetadata> = [for (m in modMetadata) if (!StringTools.endsWith(m.dirName, CONVERTED_SUFFIX)) m];

    cachedModMetadata = filtered;

    return filtered;
  }

  public static function getAllModIds():Array<String>
  {
    var modIds:Array<String> = [for (i in getAllMods()) i.id];

    return modIds;
  }

  public static function getAllModDirs():Array<String>
  {
    var modDirs:Array<String> = [for (i in getAllMods()) i.dirName];

    return modDirs;
  }

  public static function getEnabledMods():Array<ModMetadata>
  {
    var modDirs:Array<String> = Save.instance.enabledModDirs.value;

    var modMetadata:Array<ModMetadata> = getAllMods();

    var enabledMods:Array<ModMetadata> = [];

    for (item in modMetadata)
    {
      if (modDirs.indexOf(item.dirName) != -1)
      {
        enabledMods.push(item);
      }
    }

    return enabledMods;
  }

  public static function forceReloadAssets():Void
  {
    ModuleHandler.clearModuleCache();

    Polymod.clearScripts();

    refreshModCache();

    #if FEATURE_MOD_MENU
    // Load only the mods enabled in the Mod Menu.
    funkin.modding.PolymodHandler.loadEnabledMods();
    #else
    // The Mod Menu is disabled, so load all mods normally.
    funkin.modding.PolymodHandler.loadAllMods();
    #end

    SongEventRegistry.loadEventCache();
    SongRegistry.instance.loadEntries();
    LevelRegistry.instance.loadEntries();
    NoteStyleRegistry.instance.loadEntries();
    PlayerRegistry.instance.loadEntries();
    ConversationRegistry.instance.loadEntries();
    DialogueBoxRegistry.instance.loadEntries();
    SpeakerRegistry.instance.loadEntries();
    AlbumRegistry.instance.loadEntries();
    StageRegistry.instance.loadEntries();
    StickerRegistry.instance.loadEntries();
    FreeplayStyleRegistry.instance.loadEntries();
    CharacterDataParser.loadCharacterCache();
    NoteKindManager.initialize();

    ModuleHandler.loadModuleCache();
    ModuleHandler.callOnCreate();
  }
}
